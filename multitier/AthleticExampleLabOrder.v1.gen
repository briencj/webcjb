"Script for the multiphase athlete training experiment that accounts for lab order from Brien (2017)"
UNITS       [36]

"Generate a layout for the first phase"
FACTOR      [LEVELS=4] Months
&           [LEVELS=3] Athletes,Tests,Intensities,Surfaces
GENERATE    Months,Athletes,Tests & Intensities,Surfaces
RANDOMIZE   [BLOCKSTRUCTURE=Months/Athletes/Tests; SEED=2598] Intensities,Surfaces
PDESIGN     [BLOCKSTRUCTURE=Months/Athletes/Tests; \
             TREATMENTSTRUCTURE=Intensities,Surfaces]

"Get anatomy to check properties of the design"
ACANONICAL  [CRITERIA=*] !p(!f(Months/Athletes/Tests), !f(Intensities*Surfaces))

"Generate systematic design proposed by RAB for Intensities and Surfaces. 
 Uses two 3x3 mutually orthogonal Latin squares that are extended by adding 4th rows"
VARIATE     ELS3x3; !(1...3, 1...3, 2,3,1, 3,1,2)
FACTOR      [LEVELS=4] Batches
&           [LEVELS=9] Locations
&           [LEVELS=3] Intensities2; !(3(#ELS3x3))
&                      Surfaces2; !((2,3,1)3, (3,1,2)3, (1...3)6)
GENERATE    Batches,Locations
SORT        [INDEX=Batches,Intensities2,Surfaces2] \
                                               Locations,Intensities2,Surfaces2
&           [INDEX=Months,Intensities,Surfaces] Months,Athletes,Tests, \
                                                Intensities,Surfaces
&           [INDEX=Batches,Locations] Batches,Locations, \
                                      Months,Athletes,Tests,Intensities,Surfaces

"Allocate the second phase"
RANDOMIZE   [BLOCKSTRUCTURE=Batches*Locations; EXCLUDE=Batches; SEED=243526] \
                                               Athletes,Tests,Intensities,Surfaces
PDESIGN     [BLOCKSTRUCTURE=Batches/Locations; \
             TREATMENTSTRUCTURE=Months,Athletes,Tests,Intensities,Surfaces]

"Get anatomy to check properties of the design"
ACANONICAL  [CRITERIA=aefficiency, order] !p(!f(Batches*Locations),     \
                                             !f(Months/Athletes/Tests), \
                                             !f(Intensities*Surfaces))

